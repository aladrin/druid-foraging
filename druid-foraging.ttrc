# nop to use this file put #read /path/to/druid-foraging.ttrc in your tintinrc

#var {spell_table} {
{Sense Purity}{null}
{Summon Creature}{White lotus}
{Create Ley Lines}{White chalk}
{Enhance Shrine}{null}
{Flame}{Gelen sap}
{Faerie Fire}{Chutil moss}
{Heal animal}{Begnat flowers}
{Eagle Eye}{Jubjub feathers}
{Barkskin}{Yukat root}
{Natural Cleansing}{Guvana leaves}
{Tangle Vines}{Blue viper skins}
{Sandstorm}{Tumbleweed}
{Pacify animal}{Thorn apple seeds}
{Create Sacred Grove}{Holly berries}
{Purify}{null}
{Wall}{Wind-weathered rocks}
{Tree Attack}{Wood sloth droppings}
{Landslide}{Giant earthworms}
{Sticks to Snakes}{Hyach berries}
{Regrowth}{Tuwill grass}
{Call Lightning}{Lightning-fused crystals}
{Enrage Animal}{Loco root}
{Elemental Armor}{Obsidian fragments}
{Decompose}{Death cup mushrooms}
{Create Earth Mounds}{Fossils}
{Charm Animal}{Siren fruit}
{Insect Swarm}{Serem flowers}
{Animal Link}{Cahun mushrooms}
{Thunderstaff}{Lightning-struck tree bark}
{Earthquake}{Sandworm scales}
{Create Standing Stones}{Resonant crystals}
{Natures Form}{Resonant crystals}
}

#var {terrain_desc} {
{mountains}{Hiking through mountains}
{plains}{In some plains}
{bushes}{Surrounded by bushes}
{hills}{In amongst many hills}
{desert}{Crossing a desert}
{jungle}{Hacking through a jungle}
{forest}{In a light forest;In a forest;Walking in a dense forest}
{swamp}{Entrance to a Nasty Swamp;Sloshing through a swamp}
}

#var {terrain_oview} {
{mountains}{/\\}
{plains}{..}
{bushes}{++}
{hills}{\^\^}
{desert}{--}
{water}{~~}
{jungle}{::}
{forest}{//;%%%%}
{swamp}{__}
}

#var {terrain_table} {
{Begnat flowers}{swamp}
{Blue viper skins}{bushes}
{Cahun mushrooms}{swamp}
{Chutil moss}{mountains;caverns}
{Death cup mushrooms}{caverns}
{Fossils}{mountains;hills}
{Gelen sap}{forest}
{Giant earthworms}{plains}
{Guvana leaves}{jungle}
{Holly berries}{bushes}
{Hyach berries}{jungle}
{Jubjub feathers}{hills}
{Lightning-fused crystals}{hills}
{Lightning-struck tree bark}{forest;jungle}
{Loco root}{hills}
{Obsidian fragments}{mountains}
{Resonant crystals}{mountains;desert}
{Sandworm scales}{desert}
{Serem flowers}{jungle}
{Siren fruit}{desert}
{Thorn apple seeds}{bushes}
{Tumbleweed}{beach;desert}
{Tuwill grass}{plains}
{White chalk}{hills}
{White lotus}{beach;plains}
{Wind-weathered rocks}{mountains}
{Wood sloth droppings}{forest}
{Yukat root}{swamp}
}

#var {herb_list} {
Begnat flowers;
Blue viper skins;
Cahun mushrooms;
Chutil moss;
Death cup mushrooms;
Fossils;
Gelen sap;
Giant earthworms;
Guvana leaves;
Holly berries;
Hyach berries;
Jubjub feathers;
Lightning-fused crystals;
Lightning-struck tree bark;
Loco root;
Obsidian fragments;
Resonant crystals;
Sandworm scales;
Serem flowers;
Siren fruit;
Thorn apple seeds;
Tumbleweed;
Tuwill grass;
White chalk;
White lotus;
Wind-weathered rocks;
Wood sloth droppings;
Yukat root
}

#foreach $herb_list {herb} {#gag {^$herb}}

#al show_herbs_inventory {
  #foreach {*herb_inventory[%*]} {herb} {#show $herb: $herb_inventory[$herb]}
}

#al show_spell_table {
  #foreach {*spell_table[%*]} {spell} {#show $spell: $spell_table[$spell]}
}

#al show_terrain_table {
  #foreach {*terrain_table[%*]} {herb} {#show $herb: $terrain_table[$herb]}
}

#al herbs {
  #var format {%-23s%-27s%-4s%-20p%-8p};
  #if {{$created_herbs_inventory} != {yes}} {
    create_herbs_inventory;
    #line ignore herbs;
    #delay 1 spells;
  } {
    #line ignore herbs;
    #delay 1 spells;
  }
}

#al spells {
  #format heading $format {SPELL} {HERB} {#} {TERRAIN} {OVIEW};
  #echo { $heading};
  #var break {------------------------------------------------------------------------------------};
  #echo $break;
  #foreach {*terrain_desc[%*]} {terrain} {
    #foreach $terrain_desc[$terrain] desc #high {$desc} {<cfa>};
    #foreach $terrain_oview[$terrain] oview #high {$oview} {<cfa>};
  }
  #foreach {*spell_table[%*]} {spell} {
    #if {{$spell_table[$spell]} != {null}} {
      #var terrainStr {};
      #var oviewStr {};
      #unvar oviewList;
      #unvar descList;
      #foreach {$terrain_table[$spell_table[$spell]]} terrain {
        #var terrainStr {$terrainStr $terrain};
        #if {{$terrain} != {caverns} && {$terrain} != {beach}} {
          #var terrainStr {$terrainStr};
          #foreach {$terrain_oview[$terrain]} oview {
            #var oviewStr {$oviewStr $oview};
            #list oviewList add $oview;
          }
          #foreach $terrain_desc[$terrain] desc #list descList add $desc;
          #var terrainStr {$terrainStr,};
        } #var terrainStr {$terrainStr,}
      }
      #var terrainStr {$terrainStr,};
      #replace terrainStr {,,} {};
      #list oviewList simplify oviewList;
      #list descList simplify descList;
      #format spell_string $format {$spell}
        {$spell_table[$spell]}
        {$herb_inventory[$spell_table[$spell]]}
        {$terrainStr}
        {$oviewStr};
      #if {{$herb_inventory[$spell_table[$spell]]} >= {50}} {
        #echo {<cfa> $spell_string}
      } {
        #echo {<faf> $spell_string};
        #foreach $oviewList oview #high {$oview} {<faf>};
        #foreach $descList desc #high {$desc} {<faf>};
      };
    }
  }
  #echo $break;
}


#function setup_herb {
  #ac {^%1 %%1:   %%2     %2 %%3:   %%4$} {#var herb_inventory[%1] {%%2};#var herb_inventory[%2] {%%4}} {0};
}

#function destroy_herb {
  #unac {^%1 %%1:   %%2     %2 %%3:   %%4$};
}

#al create_herbs_inventory {
  #var created_herbs_inventory {yes};
  #var i 0;
  #foreach $herb_list herb {
    #switch $i {
      #case 0 {
        #var previous_herb $herb;
        #math i $i + 1;
      }
      #case 1 {
        #var null @setup_herb{$previous_herb;$herb};
        #var i 0;
      }
    }
  }
}

#al destroy_herbs_inventory {
  #var i 0;
  #foreach $herb_list herb {
    #switch $i {
      #case 0 {
        #var previous_herb $herb;
        #math i $i + 1;
      }
      #case 1 {
        #var null @destroy_herb{$previous_herb;$herb};
        #var i 0;
      }
    }
  }
}
